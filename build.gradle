plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id "com.github.ben-manes.versions" version "0.28.0"
}

group 'com.github.hauner.openapi'
version '1.0.0.M4'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

ext {
    bintrayUser = project.hasProperty ('BINTRAY_USER') ? BINTRAY_USER : 'n/a'
    bintrayKey =  project.hasProperty ('BINTRAY_KEY')  ? BINTRAY_KEY  : 'n/a'
}

repositories {
    mavenCentral()
}

dependencies {
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set ('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set ('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['api']

    pkg {
        repo = 'openapi-processor'
        name = 'openapi-processor-api'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hauner/openapi-processor-api'

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        api (MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
